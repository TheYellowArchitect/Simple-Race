extends RigidBody3D

@export_group("Car properties")
@export var wheels: Array[Node3D]
@export var brake_light_mesh: MeshInstance3D
@export var brake_light_material: StandardMaterial3D
@export var engine_power := 18
@export var downhill_multiplier := 1.3
@export var brake_power := 25
@export var tire_turn_speed := 1.3
@export var tire_max_turn_degrees := 35
@export var max_turn_curve : Curve

@export_group("Wheel properties")
@export var drag_curve : Curve
@export var grip_power := 10.0
@export var grip_front := 0.9
@export var grip_rear := 1.0
@export var grip_drift_front := 0.9
@export var grip_drift_rear := 0.9

@export_group("Air physics")
@export var air_pitch_torque := 0.2
@export var extra_gravity := 12

@export_group("Wall Collision")
@export var wall_penalty_multiplier := 0.8
@export var wall_spin_damping := 0.5

@export_category("Debug")
@export var show_debug := false

@onready var car_mass_share := mass / wheels.size()
var is_drifting := false
var is_grounded := false
var car_speed_kph := 0.0
var _prev_linear_velocity := Vector3.ZERO # Velocity right before wall collision

func _get_point_velocity(point: Vector3) -> Vector3:
	return linear_velocity + angular_velocity.cross(point - to_global(center_of_mass))

func _physics_process(delta: float) -> void:
	var throttle_input := Input.get_action_strength("throttle")
	var brake_input := Input.get_action_strength("brake")
	brake_light_material.albedo_color = Color(sign(brake_input) + 0.2, 0, 0)
	var steer_input := Input.get_axis("steer_right", "steer_left") * tire_turn_speed
	
	car_speed_kph = -global_basis.z.dot(linear_velocity) * 3.6
	for wheel in wheels:
		## Rotate wheels
		var is_front_wheel := to_local(wheel.global_position).z < 0
		if is_front_wheel:
			var steer_ratio := max_turn_curve.sample_baked(abs(car_speed_kph))
			if steer_input:
				wheel.rotation.y = clampf(wheel.rotation.y + steer_input * delta,
				deg_to_rad(-tire_max_turn_degrees * steer_ratio), 
				deg_to_rad(tire_max_turn_degrees) * steer_ratio)
			else:
				wheel.rotation.y = move_toward(wheel.rotation.y, 0, tire_turn_speed * delta)
		
		if not is_grounded: continue
		
		# Vehicle Forces
		var wheel_center := wheel.global_position
		var force_pos := wheel_center - global_position
		var wheel_forward_dir := -wheel.global_basis.z
		var tire_velocity := _get_point_velocity(wheel_center)
		var wheel_forward_velocity := wheel_forward_dir.dot(tire_velocity)
		
		# Acceleration
		var is_powered_wheel := to_local(wheel.global_position).z > 0
		if is_powered_wheel and throttle_input:
			var wheel_power_share := 0.5
			var engine_force := throttle_input * mass * engine_power * wheel_power_share * wheel_forward_dir
			if linear_velocity.y < -1: engine_force *= downhill_multiplier
			apply_force(engine_force, force_pos)
			if show_debug: DebugDraw3D.draw_arrow_ray(global_position + force_pos, engine_force, 0.01, Color.RED, 0.3, true)
		
		# Grippy steering
		var wheel_sideways_dir := wheel.global_basis.x
		var wheel_sideways_velocity := wheel_sideways_dir.dot(tire_velocity)
		
		var slip_angle = atan2(wheel_sideways_velocity, wheel_forward_velocity)
		var slip_angle_norm = remap(abs(slip_angle), 0, PI/2, 0, 1)
		
		if brake_input > 0: is_drifting = true
		if brake_input == 0 and slip_angle_norm < 0.05: is_drifting = false
		
		var grip_factor := grip_front if is_front_wheel else grip_rear
		if is_drifting: grip_factor = grip_drift_front if is_front_wheel else grip_drift_rear
		var grip_force := -wheel_sideways_velocity * wheel_sideways_dir * car_mass_share * grip_power * grip_factor
		apply_force(grip_force, force_pos)
		if show_debug: DebugDraw3D.draw_arrow_ray(global_position + force_pos, grip_force, 0.01, Color.YELLOW, 0.3, true)
		
		# Decelartion
		var force_basis := wheel.global_basis.z * car_mass_share
		var drag := drag_curve.sample_baked(abs(car_speed_kph))
		var drag_force := force_basis * drag * (2 - throttle_input) * signf(car_speed_kph)
		var brake_modifier := 0.4 if car_speed_kph < 1.0 else 1.0
		var braking_force := force_basis * brake_power * brake_modifier * brake_input
		apply_force(drag_force + braking_force, force_pos)
		if show_debug: DebugDraw3D.draw_arrow_ray(global_position + force_pos, drag_force + braking_force, 0.01, Color.ORANGE, 0.3, true)

const WALL_HIT_COOLDOWN: float = 0.3
const WALL_HIT_MIN_VELOCITY: float = 0.7
var _last_major_wall_hit_timestamp: float = 0.0
var _registered_wall_collisions: Array[int]
var _pre_collision_velocity: Vector3 = Vector3.ZERO
signal entered_major_wall_collision(impact_velocity: float)
signal exited_wall_collision()
signal entered_minor_wall_collision()
func _integrate_forces(state: PhysicsDirectBodyState3D) -> void:
	is_grounded = false
	var max_impact := 0.0

	var current_contact_ids: Array[int] = []
	for i in state.get_contact_count():
		var normal: Vector3 = state.get_contact_local_normal(i)
		if abs(normal.y) < 0.3:  # wall only
			current_contact_ids.append(state.get_contact_collider_id(i))

	for i in state.get_contact_count():
		var collider_id: int = state.get_contact_collider_id(i)
		var normal: Vector3 = state.get_contact_local_normal(i)

		if normal.y > 0.5:
			is_grounded = true

		if abs(normal.y) < 0.3:
			var impact_velocity: float = abs(_pre_collision_velocity.dot(normal))
			if impact_velocity > max_impact:
				max_impact = impact_velocity

			if not _registered_wall_collisions.has(collider_id):
				_registered_wall_collisions.append(collider_id)

			var now := Time.get_ticks_msec() / 1000.0
			if impact_velocity > WALL_HIT_MIN_VELOCITY and (now - _last_major_wall_hit_timestamp) > WALL_HIT_COOLDOWN:
				_last_major_wall_hit_timestamp = now
				entered_major_wall_collision.emit(impact_velocity)
			elif impact_velocity < WALL_HIT_MIN_VELOCITY:
				if (impact_velocity > 0.1):
					entered_minor_wall_collision.emit(impact_velocity)

	var exited_ids: Array = []
	for registered_id: int in _registered_wall_collisions:
		if not current_contact_ids.has(registered_id):
			exited_ids.append(registered_id)
	for exited_id: int in exited_ids:
		_registered_wall_collisions.erase(exited_id)
		exited_wall_collision.emit()

	if max_impact > 0.5:
		var car_forward_dir := -global_basis.z
		var current_forward_speed := state.linear_velocity.dot(car_forward_dir)
		var speed_reduction := max_impact * wall_penalty_multiplier
		var new_forward_speed := move_toward(current_forward_speed, 0.0, speed_reduction)
		var actual_reduction := current_forward_speed - new_forward_speed
		state.linear_velocity -= car_forward_dir * actual_reduction
		state.angular_velocity *= wall_spin_damping

	if not is_grounded:
		linear_damp = 0.0
		apply_torque(-global_basis.x * air_pitch_torque * mass)
		apply_central_force(Vector3.DOWN * extra_gravity * mass)
	else:
		linear_damp = 0.1
	
	## Frontal high speed collisions are already processed in THE START of this frame
	## so to take the real/accurate pre-collision velocity
	## we fetch it from the previous frame.
	## This is where the accurate velocity is stored for the aforementioned fetching to be used far above :)
	_pre_collision_velocity = state.linear_velocity
